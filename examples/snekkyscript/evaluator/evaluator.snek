import "std/Std";

let Evaluator = func() {

    let std = Std();

    mut executeBlock = null;
    mut currentBlock = null;

    mut getVariableScope = null;
    getVariableScope = func(name, block) {
        return if block.getVariable(name) != null {
            block
        } else if block.parent != null {
            getVariableScope(name, block.parent)
        } else {
            null
        };
    };

    mut evalExpression = null;
    evalExpression = func(expr) {
        return when expr.type {
            NodeType.Add => evalExpression(expr.left) + evalExpression(expr.right)
            NodeType.Subtract => evalExpression(expr.left) - evalExpression(expr.right)
            NodeType.Multiply => evalExpression(expr.left) * evalExpression(expr.right)
            NodeType.Divide => evalExpression(expr.left) / evalExpression(expr.right)
            NodeType.Modulo => evalExpression(expr.left) % evalExpression(expr.right)
            NodeType.LessThan => evalExpression(expr.left) < evalExpression(expr.right)
            NodeType.GreaterThan => evalExpression(expr.left) > evalExpression(expr.right)
            NodeType.Equals => evalExpression(expr.left) == evalExpression(expr.right)
            NodeType.Ident => {
                if (std.isBuiltIn(expr.value)) {
                    std.getBuiltIn(expr.value)
                } else {
                    getVariableScope(expr.value, currentBlock).getVariable(expr.value)
                }
            }
            NodeType.Expression => evalExpression(expr.value)
            NodeType.Call => {
                let target = evalExpression(expr.target);
                let arguments = expr.arguments.map(func(a) {
                    return evalExpression(a);
                });

                target(arguments)
            }
            else => expr.value
        };
    };

    let executeInstruction = func(ins) {
        when ins.type {
            NodeType.Variable => {
                let variableScope = getVariableScope(ins.name, currentBlock);
                if variableScope != null {
                    variableScope.setVariable(ins.name, evalExpression(ins.value.value));
                } else {
                    currentBlock.setVariable(ins.name, evalExpression(ins.value.value));
                }                
            }                
            NodeType.If => {
                let condition = evalExpression(ins.condition.value);
                if condition {
                    executeBlock(ins.consequence);
                } else if (ins.alternative != null) {
                    executeBlock(ins.alternative);
                }                
            }
            NodeType.While => {
                mut condition = evalExpression(ins.condition.value);
                while condition {
                    executeBlock(ins.body);

                    condition = evalExpression(ins.condition.value);
                }                      
            }
            NodeType.Expression => evalExpression(ins.value);
        }
    };

    executeBlock = func(block) {
        let lastBlock = currentBlock;
        currentBlock = block;

        mut i = 0;
        while i < block.nodes.length() {
            executeInstruction(block.nodes[i]);

            i += 1;
        }

        currentBlock = lastBlock;
    };

    let execute = func(ast) {
        executeBlock(ast);
    };

    return {
        execute: execute,
        std: func() {
            return std;
        }
    };
};