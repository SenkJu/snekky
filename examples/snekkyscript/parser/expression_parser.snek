let ExpressionParser = func(lexer, parser) {
    mut factor = null;
    mut numeric = null;
    mut comparison = null;
    mut term = null;

    factor = func() {
        let currentToken = parser.currentToken();

        return when currentToken.type {
            TokenType.LParen => {
                parser.nextToken();
                let comp = comparison();
                parser.nextToken();

                comp  
            }
            TokenType.Ident => {
                let ident = IdentNode(currentToken.literal);
                parser.nextToken();

                ident
            }
            TokenType.String => {
                let string = StringNode(currentToken.literal);
                parser.nextToken();

                string                
            }
            TokenType.Number => {
                let number = NumberNode(Number.parse(currentToken.literal));
                parser.nextToken();

                number 
            }
            else => {
                Sys.println("Unexpected token `" >< parser.currentToken().literal >< "`");
                Sys.exit(0);

                null
            }
        };
    };

    term = func() {
        mut left = factor();

        while true {
            let type = when parser.currentToken().type {
                TokenType.Asterisk => NodeType.Multiply
                TokenType.Slash => NodeType.Divide
                TokenType.Percent => NodeType.Modulo
                else => {
                    return left;
                }
            };

            parser.nextToken();
            let right = factor();
            left = OperatorNode(type, left, right);
        }

        return left;
    };

    numeric = func() {
        mut left = term();

        while true {
            let type = when parser.currentToken().type {
                TokenType.Plus => NodeType.Add
                TokenType.Minus => NodeType.Subtract
                else => {
                    return left;
                }
            };

            parser.nextToken();
            let right = term();
            left = OperatorNode(type, left, right);
        }

        return left;
    };


    comparison = func() {
        let left = numeric();

        let type = when parser.currentToken().type {
            TokenType.LessThan => NodeType.LessThan
            TokenType.GreaterThan => NodeType.GreaterThan
            TokenType.Equals => NodeType.Equals
            else => {
                return left;
            }
        };

        parser.nextToken();
        let right = numeric();
        return OperatorNode(type, left, right);
    };

    let parseExpression = func() {
        return ExpressionNode(comparison());
    };

    return {
        parseExpression: parseExpression
    };
};